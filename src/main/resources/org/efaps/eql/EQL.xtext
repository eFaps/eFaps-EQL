grammar org.efaps.eql.EQL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate eQL "http://www.efaps.org/EQL"

Statement:
    (printPart=PrintPart)? (queryPart=QueryPart)? (wherePart=WherePart)? (selectPart=SelectPart)? (orderPart=OrderPart)?;

PrintPart:
    'print' oid=OID
;

QueryPart:
    'query' ('type' types+=TYPE) (',' types+=TYPE)*;

WherePart:
    'where' wheres+=OneWhere ('and' wheres+=OneWhere)*;

OneWhere:
    attribute=ATTRIBUTE => comparison=Comparison (value=(Value | OID | STRING)
    | '(' values+=(Value | OID | STRING) (","  values+=(Value | OID | STRING))* ')');

Value:
    KEY;

OID:
    KEY+ "."+ KEY;

enum Comparison:
    EQUAL='==' | EQUAL='eq' | LESS='<' | GREATER='>' | UNEQUAL='!=' | LIKE='like' | IN='in';

SelectPart:
    'select' selects+=OneSelect (',' selects+=OneSelect)*;

OneSelect:
    select=SELECTSTR (=> 'as' alias=ALIAS)?;

OrderPart:
    'order by' oneOrder+=OneOrder (',' oneOrder+=OneOrder)*;

OneOrder:
    key=(ALIAS | KEY) (desc?=('desc'))?;

terminal KEY:
    ('0'..'9')+;

terminal ALIAS:
    ('a'..'z' | 'A'..'Z')+;

ATTRIBUTE:
    (ALIAS | '_')+;

terminal fragment KARET:
    '[' ('0'..'9' | 'a'..'z' | 'A'..'Z' | '.' | '_' | '#')+ ']';

terminal SELECTSTR:
    ('a'..'z') ('a'..'z' | 'A'..'Z' | '.' | '_' | KARET)*;

terminal TYPE:
    ('A'..'Z') ('a'..'z' | 'A'..'Z' | '_')*;

terminal STRING:
    '"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
    "'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";
