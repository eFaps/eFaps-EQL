grammar org.efaps.eql.EQL hidden(WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate eQL "http://www.efaps.org/EQL"

Statement:
    (execPart=ExecPart) | (printPart=PrintPart) | (queryPart=QueryPart) | (updatePart=UpdatePart) | (deletePart=DeletePart) | (insertPart=InsertPart);

DeletePart:
    'delete' ((('obj' | 'object')? oid=OID) | 'list' '(' oids+=OID (',' oids+=OID)* ')' | queryPart=ObjQueryPart)
;

InsertPart:
    'insert' 'type' type=TYPE 'set' (updates+=OneUpdate) (',' updates+=OneUpdate)*
;

UpdatePart:
    'update' ((('obj' | 'object')? oid=OID) | 'list' '(' oids+=OID (',' oids+=OID)* ')' | queryPart=ObjQueryPart) 'set' (updates+=OneUpdate) (',' updates+=OneUpdate)*
;

ObjQueryPart:
    'query' ('type' types+=TYPE) (',' types+=TYPE)* (wherePart=WherePart)? (limitPart=LimitPart)?;

OneUpdate:
    attribute=ATTRIBUTE '=' value=(KEY | STRING)
;

ExecPart:
    ('exec' | 'execute') className=CLASSNAME (parameters+=(KEY | STRING) (',' parameters+=(KEY | STRING))*)? ('select'
    execSelect+=ExecSelect (',' execSelect+=ExecSelect)*)? (orderPart=OrderPart)?;

ExecSelect:
    select=KEY (=> 'as' alias=ALIAS)?;

PrintPart:
    'print' ((('obj' | 'object')? oid=OID) | 'list' '(' oids+=OID (',' oids+=OID)* ')') (selectPart=SelectPart) (orderPart=OrderPart)?;

QueryPart:
    ('print')? 'query' ('type' types+=TYPE) (',' types+=TYPE)* (wherePart=WherePart)? (limitPart=LimitPart)?  (selectPart=SelectPart) (orderPart=OrderPart)?;

WherePart:
    'where' wheres+=OneWhere ('and' wheres+=OneWhere)*;

OneWhere:
    (attribute=ATTRIBUTE | select=SELECTSTR) => comparison=Comparison (value=(Value | OID | STRING) | '(' values+=(Value | OID | STRING)
    ("," values+=(Value | OID | STRING))* ')' | nestedQueryPart=NestedQueryPart );

NestedQueryPart:
    '(' 'query' ('type' types+=TYPE) (',' types+=TYPE)* (wherePart=WherePart)? (selectPart=SelectPart)? ')';

Value:
    KEY;

OID:
    KEY+ "."+ KEY;

enum Comparison:
    EQUAL='==' | EQUAL='eq' | LESS='<' | GREATER='>' | UNEQUAL='!=' | LIKE='like' | IN='in' | NOTIN='not in';

SelectPart:
    'select' selects+=OneSelect (',' selects+=OneSelect)*;

OneSelect:
    select=SELECTSTR (=> 'as' alias=ALIAS)?;

OrderPart:
    'order by' oneOrder+=OneOrder (',' oneOrder+=OneOrder)*;

OneOrder:
    key=(ALIAS | KEY) ('asc' |(desc?=('desc')))?;

LimitPart:
    'limit' value=KEY;

terminal WS:
    (' ' | '\t' | '\r' | '\n')+;

terminal CLASSNAME:
     (('a'..'z') ('A'..'Z' | 'a'..'z' | '0'..'9')* ('.'))+ ('A'..'Z') ('A'..'Z' | 'a'..'z' | '0'..'9')+;

terminal KEY:
    ('0'..'9')+;

terminal ALIAS:
    ('a'..'z' | 'A'..'Z')+;

ATTRIBUTE:
    (ALIAS | '_')+;

terminal fragment KARET:
    '[' ('0'..'9' | 'a'..'z' | 'A'..'Z' | '.' | '_' | '-' | '/' | '#')+ ']';

terminal SELECTSTR:
    ('a'..'z') ('a'..'z' | 'A'..'Z' | '.' | '_' | KARET)*;

terminal fragment HEX:
    ('a'..'f' | '0'..'9');

terminal fragment UUID:
    HEX HEX HEX HEX HEX HEX HEX HEX '-' HEX HEX HEX HEX '-' HEX HEX HEX HEX '-' HEX HEX HEX HEX '-' HEX HEX HEX HEX HEX HEX HEX HEX HEX HEX HEX HEX;

terminal TYPE:
    (('A'..'Z') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*) | UUID;

terminal STRING:
    '"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
    "'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

