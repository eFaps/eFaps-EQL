grammar org.efaps.eql.EQL hidden(WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate eQL "http://www.efaps.org/EQL"

Statement:
    (execPart=ExecPart)? (printPart=PrintPart)? (queryPart=QueryPart)? (wherePart=WherePart)? (selectPart=SelectPart)?
    (orderPart=OrderPart)? (updatePart=UpdatePart)?;

UpdatePart:
    'update' oid=OID
;

ExecPart:
    ('exec' | 'execute') className=CLASSNAME (parameters+=(KEY | STRING) (',' parameters+=(KEY | STRING))*)? ('select'
    execSelect+=ExecSelect (',' execSelect+=ExecSelect)*)?;

ExecSelect:
    select=KEY (=> 'as' alias=ALIAS)?;

PrintPart:
    'print' oid=OID;

QueryPart:
    'query' ('type' types+=TYPE) (',' types+=TYPE)*;

WherePart:
    'where' wheres+=OneWhere ('and' wheres+=OneWhere)*;

OneWhere:
    (attribute=ATTRIBUTE | select=SELECTSTR) => comparison=Comparison (value=(Value | OID | STRING) | '(' values+=(Value | OID | STRING)
    ("," values+=(Value | OID | STRING))* ')');

Value:
    KEY;

OID:
    KEY+ "."+ KEY;

enum Comparison:
    EQUAL='==' | EQUAL='eq' | LESS='<' | GREATER='>' | UNEQUAL='!=' | LIKE='like' | IN='in';

SelectPart:
    'select' selects+=OneSelect (',' selects+=OneSelect)*;

OneSelect:
    select=SELECTSTR (=> 'as' alias=ALIAS)?;

OrderPart:
    'order by' oneOrder+=OneOrder (',' oneOrder+=OneOrder)*;

OneOrder:
    key=(ALIAS | KEY) (desc?=('desc'))?;

terminal WS:
    (' ' | '\t' | '\r' | '\n')+;

terminal CLASSNAME:
     (('a'..'z') ('A'..'Z' | 'a'..'z' | '0'..'9')* ('.'))+ ('A'..'Z') ('A'..'Z' | 'a'..'z' | '0'..'9')+;

terminal KEY:
    ('0'..'9')+;

terminal ALIAS:
    ('a'..'z' | 'A'..'Z')+;

ATTRIBUTE:
    (ALIAS | '_')+;

terminal fragment KARET:
    '[' ('0'..'9' | 'a'..'z' | 'A'..'Z' | '.' | '_' | '#')+ ']';

terminal SELECTSTR:
    ('a'..'z') ('a'..'z' | 'A'..'Z' | '.' | '_' | KARET)*;

terminal TYPE:
    ('A'..'Z') ('a'..'z' | 'A'..'Z' | '_')*;

terminal STRING:
    '"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
    "'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

